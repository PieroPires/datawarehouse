USE [VAGAS_DW] ;

IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLAP_Carga_Canal_Reclame_Aqui' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE VAGAS_DW.SPR_OLAP_Carga_Canal_Reclame_Aqui
GO

-- =============================================
-- Author: Fiama
-- Create date: 25/04/2017
-- Description: Procedure para carga das tabelas temporárias (BD Stage) para alimentação do DW
-- =============================================

CREATE PROCEDURE VAGAS_DW.SPR_OLAP_Carga_Canal_Reclame_Aqui

AS
SET NOCOUNT ON

	-- LIMPEZA DA TABELA:
	TRUNCATE TABLE [STAGE].[VAGAS_DW].[TMP_CANAL_RECLAME_AQUI] ;

	DECLARE	@CMD VARCHAR(8000) ,
			@MSG VARCHAR(8000)

	CREATE TABLE #TMP_LOG_ERRO (ID_LOG_ERRO SMALLINT IDENTITY PRIMARY KEY ,
								ERRO VARCHAR(8000)) 
	
	CREATE TABLE #TMP_AUXILIAR_RECLAME_AQUI (
		[DATA_RECEBIMENTO] VARCHAR(255) NULL,
		[NUMERO_PROTOCOLO] VARCHAR(255) NULL,
		[EMAIL] VARCHAR(255) NULL ,
		[CPF] VARCHAR (20) NULL ,
		[ASSUNTO] VARCHAR(255) NULL ,
		[MENSAGEM_ENVIADA] VARCHAR(255) NULL,
		[AVALIOU] VARCHAR(255) NULL,
		[NOTA] VARCHAR(255) NULL,
		[VOLT_NEGOCIO] VARCHAR(255) NULL,
		[RESOLVIDO] VARCHAR(255) NULL ,
		[CANAL] VARCHAR(100) ,
		[AVALIOU_DESCR] VARCHAR(50) NULL);


	DECLARE	@ID_CONTROLE_SPREADSHEET SMALLINT ,
			@SHEET_NAME VARCHAR(100) , 
			@NOME_CONTROLE VARCHAR(100) ;

	SET	@ID_CONTROLE_SPREADSHEET = (SELECT	MIN(ID_CONTROLE_SPREADSHEET)
									FROM	[VAGAS_DW].[CONTROLE_SPREADSHEET]
									WHERE	NOME_CONTROLE = 'CANAIS_RELACIONAMENTO'
											AND SHEET_NAME = 'Reclame Aqui') ;

	SET	@SHEET_NAME = (SELECT TOP 1 SHEET_NAME
					   FROM	[VAGAS_DW].[CONTROLE_SPREADSHEET]
					   WHERE NOME_CONTROLE = 'CANAIS_RELACIONAMENTO'
							AND SHEET_NAME = 'Reclame Aqui') 
					   
	SET @NOME_CONTROLE = (SELECT TOP 1 NOME_CONTROLE
						  FROM	[VAGAS_DW].[CONTROLE_SPREADSHEET]
						  WHERE	NOME_CONTROLE = 'CANAIS_RELACIONAMENTO')


	WHILE @ID_CONTROLE_SPREADSHEET IN (SELECT ID_CONTROLE_SPREADSHEET FROM [VAGAS_DW].[CONTROLE_SPREADSHEET] WHERE NOME_CONTROLE = 'CANAIS_RELACIONAMENTO' AND SHEET_NAME = 'Reclame Aqui')
	BEGIN
		
		-- Carregar tabela baseado na Planilha do Google Drive
		SET @CMD = 'Z:\Scripts\Python3\python Z:\Scripts\Scripts_Python\Exportacao_Arquivos_Google_Drive\LerPlanilhaGoogleDrive.py ' + 
				' "' + CONVERT(VARCHAR(3), @ID_CONTROLE_SPREADSHEET) + '" "TMP_CANAL_RECLAME_AQUI" "1"'

		INSERT INTO #TMP_LOG_ERRO (ERRO)
		EXEC MASTER.DBO.XP_CMDSHELL @CMD
		print @CMD

		INSERT INTO #TMP_AUXILIAR_RECLAME_AQUI
		SELECT	[3],[4],[5],[6],[7],[8],[9],[10],[11],[12], @SHEET_NAME, [9]
		FROM	[STAGE].[VAGAS_DW].[TMP_CANAL_RECLAME_AQUI]
		WHERE	[index] > 0 ;

		-- Tratar erros ocorridos na rotina do Python	
		IF EXISTS ( SELECT * FROM #TMP_LOG_ERRO WHERE CHARINDEX('ERROR_MESSAGE',ERRO) > 0 ) 
		BEGIN 
			SET @MSG = 'ERRO ocorrido na importação dos dados no ID_CONTROLE_SPREADSHEET :' + ' ' + CONVERT(VARCHAR(3), @ID_CONTROLE_SPREADSHEET) 
			RAISERROR(@MSG , 16, 1) 
		END

		TRUNCATE TABLE #TMP_LOG_ERRO

		SET @ID_CONTROLE_SPREADSHEET +=	(SELECT TOP 1 (ID_CONTROLE_SPREADSHEET - @ID_CONTROLE_SPREADSHEET)
										 FROM	[VAGAS_DW].[CONTROLE_SPREADSHEET]
										 WHERE	NOME_CONTROLE = 'CANAIS_RELACIONAMENTO'
												AND SHEET_NAME = 'Reclame Aqui'
												AND ID_CONTROLE_SPREADSHEET > @ID_CONTROLE_SPREADSHEET)

		SET @SHEET_NAME = (SELECT TOP 1 SHEET_NAME
						   FROM	  [VAGAS_DW].[CONTROLE_SPREADSHEET]
						   WHERE  NOME_CONTROLE = 'CANAIS_RELACIONAMENTO'
								  AND ID_CONTROLE_SPREADSHEET = @ID_CONTROLE_SPREADSHEET)
	END

	-- REMOÇÃO DE LINHAS EM BRANCO:
	DELETE	FROM #TMP_AUXILIAR_RECLAME_AQUI
	FROM	#TMP_AUXILIAR_RECLAME_AQUI
	WHERE	ISNULL(DATA_RECEBIMENTO, '') = '' 
			AND ISNULL(NUMERO_PROTOCOLO, '') = '' 
			AND ISNULL(EMAIL, '') = '' 
			AND ISNULL(CPF, '') = '' 
			AND ISNULL(ASSUNTO, '') = '' 
			AND ISNULL(MENSAGEM_ENVIADA, '') = '' 
			AND ISNULL(AVALIOU, '') = '' 
			AND ISNULL(NOTA, '') = '' 
			AND ISNULL(VOLT_NEGOCIO, '') = '' 
			AND ISNULL(RESOLVIDO, '') = '' ;


-------------------------------------
-- CONSOLIDADO CANAIS RELACIONAMENTO:
-------------------------------------
	INSERT INTO [VAGAS_DW].[CANAIS_RELACIONAMENTO]
	SELECT	--A.DATA_RECEBIMENTO ,
		--	LTRIM(RTRIM(REPLACE(REVERSE(SUBSTRING(REVERSE(A.DATA_RECEBIMENTO), 1, CHARINDEX('/', REVERSE(A.DATA_RECEBIMENTO)))), '/', ''))) AS ANO ,
		--	LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 4, 2))) AS MES ,
		--	LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 1, 2))) AS DIA ,
			CASE WHEN LEN(LTRIM(RTRIM(REPLACE(REVERSE(SUBSTRING(REVERSE(A.DATA_RECEBIMENTO), 1, CHARINDEX('/', REVERSE(A.DATA_RECEBIMENTO)))), '/', '')))) = 4 AND CONVERT(SMALLINT, LTRIM(RTRIM(REPLACE(REVERSE(SUBSTRING(REVERSE(A.DATA_RECEBIMENTO), 1, CHARINDEX('/', REVERSE(A.DATA_RECEBIMENTO)))), '/', '')))) BETWEEN 2000 AND 2100 AND LEN(LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 4, 2))))= 2 AND CONVERT(SMALLINT, LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 4, 2)))) BETWEEN 1 AND 12 AND LEN(LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 1, 2)))) = 2 AND CONVERT(SMALLINT, LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 1, 2)))) BETWEEN 1 AND 31 THEN DATEFROMPARTS(CONVERT(SMALLINT, LTRIM(RTRIM(REPLACE(REVERSE(SUBSTRING(REVERSE(A.DATA_RECEBIMENTO), 1, CHARINDEX('/', REVERSE(A.DATA_RECEBIMENTO)))), '/', '')))), CONVERT(SMALLINT, LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 4, 2)))), CONVERT(SMALLINT, LTRIM(RTRIM(SUBSTRING(A.DATA_RECEBIMENTO, 1, 2))))) END AS DATA_RECEBIMENTO ,
			NUMERO_PROTOCOLO ,
			EMAIL ,
			CPF ,
			ASSUNTO ,
			MENSAGEM_ENVIADA ,
			CASE WHEN LTRIM(RTRIM(AVALIOU)) = 'SIM' THEN 'SIM' ELSE 'NÃO' END AS AVALIOU ,
			CASE WHEN LTRIM(RTRIM(AVALIOU)) = 'SIM' THEN CONVERT(TINYINT, NOTA) ELSE NULL END AS NOTA ,
			CASE WHEN VOLT_NEGOCIO LIKE '%S%' THEN 1 ELSE 0 END AS VOLT_NEGOCIO ,
			CASE WHEN RESOLVIDO LIKE '%S%' THEN 1 ELSE 0 END AS RESOLVIDO ,
			CANAL ,
			AVALIOU_DESCR
	FROM	#TMP_AUXILIAR_RECLAME_AQUI AS A
	ORDER BY
			DATA_RECEBIMENTO ASC ,
			NUMERO_PROTOCOLO ASC ;


-- LIMPEZA DAS TABELAS TEMPORÁRIAS LOCAIS:
DROP TABLE #TMP_LOG_ERRO ;
DROP TABLE #TMP_AUXILIAR_RECLAME_AQUI ;