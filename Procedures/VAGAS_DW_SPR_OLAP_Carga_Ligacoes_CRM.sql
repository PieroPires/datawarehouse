USE VAGAS_DW
GO

IF EXISTS ( SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLAP_Carga_Ligacoes_CRM' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE VAGAS_DW.SPR_OLAP_Carga_Ligacoes_CRM
GO

-- =============================================
-- Author: Luiz Fernando Braz
-- Create date: 29/09/2015
-- Description: Procedure para carga das tabelas temporárias (BD Stage) para alimentação do DW
-- =============================================

CREATE PROCEDURE VAGAS_DW.SPR_OLAP_Carga_Ligacoes_CRM 
AS
SET NOCOUNT ON

-- Limpar dados da tabela fato
DELETE VAGAS_DW.LIGACOES_CRM
FROM VAGAS_DW.LIGACOES_CRM A
WHERE EXISTS ( SELECT 1 FROM VAGAS_DW.TMP_LIGACOES_CRM
				WHERE ID_LIGACAO = A.ID_LIGACAO )

-- var Grupo
UPDATE VAGAS_DW.TMP_LIGACOES_CRM SET GRUPO_VENDEDOR = B.GRUPO_VENDEDOR
FROM VAGAS_DW.TMP_LIGACOES_CRM A
INNER JOIN VAGAS_DW.GRUPO_VENDEDORES B ON B.VENDEDOR = A.USUARIO_RESPONSAVEL

-- CLIENTE ASSOCIADO À OPORTUNIDADE
UPDATE VAGAS_DW.TMP_LIGACOES_CRM SET CONTA = B.CONTA
FROM VAGAS_DW.TMP_LIGACOES_CRM A
INNER JOIN VAGAS_DW.OPORTUNIDADES B ON B.OPORTUNIDADE = A.REFERENTE_A
WHERE A.TIPO_CONTATO = 'Opportunities'

UPDATE VAGAS_DW.TMP_LIGACOES_CRM SET CONTA = A.REFERENTE_A
FROM VAGAS_DW.TMP_LIGACOES_CRM A
WHERE A.TIPO_CONTATO IN ('Accounts','Leads')

INSERT INTO VAGAS_DW.LIGACOES_CRM (ID_LIGACAO,NOME,DESCRICAO,DATA_CADASTRO,DATA_ALTERACAO,USUARIO_CADASTRO,USUARIO_ALTERACAO,
									USUARIO_RESPONSAVEL,DURACAO_HORAS,DURACAO_MINUTOS,DATA_INICIO,DATA_FIM,STATUS,TIPO,CLASSIFICACAO,
									REFERENTE_A,GRUPO_VENDEDOR,TIPO_CONTATO,CONTA)
SELECT ID_LIGACAO,
	   NOME,
	   DESCRICAO,
	   DATA_CADASTRO,
	   DATA_ALTERACAO,
	   USUARIO_CADASTRO,
	   USUARIO_ALTERACAO,
	   USUARIO_RESPONSAVEL,
	   DURACAO_HORAS,
	   DURACAO_MINUTOS,
	   DATA_INICIO,
	   DATA_FIM,
	   CASE WHEN STATUS = 'Held' THEN 'Realizado'
			WHEN STATUS = 'Planned' THEN 'Planejado'
			WHEN STATUS = 'Not Held' THEN 'Não Realizado'
			ELSE 'Não Classificado' END,
	   CASE WHEN TIPO = 'Inbound' THEN 'Recebida'
			WHEN TIPO = 'Outbound' THEN 'Efetuada'
			ELSE 'Não Classificado' END,
	   CASE WHEN CHARINDEX('#Prospecção',NOME) > 0 OR CHARINDEX('#Prospecção',DESCRICAO) > 0 THEN 'Prospecção'
			WHEN CHARINDEX('#Relacionamento',NOME) > 0 OR CHARINDEX('#Relacionamento',DESCRICAO) > 0 THEN 'Relacionamento'
			ELSE 'Não Classificado' END,
	   REFERENTE_A,
	   GRUPO_VENDEDOR,
	   TIPO_CONTATO,
	   CONTA
FROM VAGAS_DW.TMP_LIGACOES_CRM
WHERE FLAG_DELETADO = 0


