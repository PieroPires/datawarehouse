USE VAGAS_DW
GO

IF EXISTS ( SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLAP_Calculo_Media_Demandas_Inteligencia_Negocios' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE VAGAS_DW.SPR_OLAP_Calculo_Media_Demandas_Inteligencia_Negocios
GO

-- =============================================
-- Author: Luiz Fernando Braz
-- Create date: 20/08/2016
-- Description: Procedure para cálculo da MEDIA_DIAS_ENTREGA para a equipe IN
-- =============================================

CREATE PROCEDURE VAGAS_DW.SPR_OLAP_Calculo_Media_Demandas_Inteligencia_Negocios 
AS
SET NOCOUNT ON

-- SUB-DEMANDAS CONCLUIDAS
SELECT A.NUMERO_DEMANDA,
	   B.NOME_DEMANDA AS DEMANDA_MAE,
	   B.DATA_CADASTRAMENTO AS DATA_CADASTRAMENTO_MAE,
	   A.DATA_CONCLUSAO AS DATA_ENTREGA,
	   DATEDIFF(DAY,ISNULL(C.DATA_CONCLUSAO,B.DATA_CADASTRAMENTO),A.DATA_CONCLUSAO) AS DIAS_ENTREGA
INTO #TMP_DEMANDAS_DIAS_ENTREGA
FROM VAGAS_DW.DEMANDAS_EQUIPES A
OUTER APPLY ( SELECT TOP 1 * FROM VAGAS_DW.DEMANDAS_EQUIPES
			  WHERE NOME_DEMANDA = A.NOME_DEMANDA_ROOT 
			  AND FLAG_ROOT = 1
			  AND DATA_CADASTRAMENTO <= A.DATA_CADASTRAMENTO
			  ORDER BY DATA_CADASTRAMENTO DESC ) B
OUTER APPLY ( SELECT TOP 1 *
			  FROM VAGAS_DW.DEMANDAS_EQUIPES 
			  WHERE NOME_DEMANDA_ROOT = A.NOME_DEMANDA_ROOT
			  AND CICLO = A.CICLO
			  AND FLAG_ROOT = 0
			  AND TIPO_SUB_TAREFA IN ('Cubo','Relatório','Relatório pontual','Mailing','Estudos para Assessoria',
									  'Apresentação de resultados','Desenho do questionário')
			  AND STATUS_DEMANDA <> 'Cancelada'
			  AND DATA_CONCLUSAO < A.DATA_CONCLUSAO
			  ORDER BY DATA_CONCLUSAO DESC ) C
WHERE A.FLAG_ROOT = 0 
AND A.FLAG_ATRELADO_MARCO = 0
AND A.TIPO_SUB_TAREFA IN ('Cubo','Relatório','Relatório pontual','Mailing','Estudos para Assessoria','Apresentação de resultados','Desenho do questionário')
AND A.DATA_CONCLUSAO IS NOT NULL
AND A.NOME_PROJETO = 'Inteligência de Negócios'
AND A.STATUS_DEMANDA <> 'Cancelada'

UNION ALL 

SELECT NULL AS NUMERO_DEMANDA,
	   A.NOME_DEMANDA AS DEMANDA_MAE,
	   A.CICLO,
	   NULL AS DATA_ENTREGA,
	   DATEDIFF(DAY,DATA_CADASTRAMENTO,GETDATE()) AS DIAS_ENTREGA
FROM VAGAS_DW.DEMANDAS_EQUIPES A
WHERE FLAG_ROOT = 1 
AND STATUS_DEMANDA IN ('Aberta','In Progress')
AND FLAG_ATRELADO_MARCO = 0 
AND NOME_PROJETO = 'Inteligência de Negócios'


-- ATUALIZAR MÉDIAS DIAS ENTREGA PARA SUB-TASKS CONCLUIDAS
-- UPDATE VAGAS_DW.DEMANDAS_EQUIPES SET MEDIA_DIAS_ENTREGA = NULL,DATA_ULT_ENTREGA = NULL,DIAS_CONCLUSAO = NULL
UPDATE VAGAS_DW.DEMANDAS_EQUIPES SET MEDIA_DIAS_ENTREGA = B.DIAS_ENTREGA
FROM VAGAS_DW.DEMANDAS_EQUIPES A
INNER JOIN #TMP_DEMANDAS_DIAS_ENTREGA B ON B.NUMERO_DEMANDA = A.NUMERO_DEMANDA
AND NOME_PROJETO = 'Inteligência de Negócios'

-- ATUALIZAR MEDIA, DATA ULT ENTREGA E DIAS PARA CONCLUSÃO DAS TAREFAS MAE
UPDATE VAGAS_DW.DEMANDAS_EQUIPES SET MEDIA_DIAS_ENTREGA = ISNULL(B.MEDIA_DIAS_ENTREGA,DATEDIFF(DAY,DATA_CADASTRAMENTO,CASE WHEN STATUS_DEMANDA IN ('Aberta','In Progress') THEN GETDATE() ELSE DATA_CONCLUSAO END )),
									 DATA_ULT_ENTREGA = B.DATA_ULT_ENTREGA,
									 DIAS_CONCLUSAO = DATEDIFF(DAY,DATA_CADASTRAMENTO,CASE WHEN STATUS_DEMANDA IN ('Aberta','In Progress') THEN GETDATE() ELSE DATA_CONCLUSAO END )
FROM VAGAS_DW.DEMANDAS_EQUIPES A
OUTER APPLY ( SELECT AVG(DIAS_ENTREGA) AS MEDIA_DIAS_ENTREGA,
					 MAX(DATA_ENTREGA) AS DATA_ULT_ENTREGA
			  FROM #TMP_DEMANDAS_DIAS_ENTREGA
			  WHERE DEMANDA_MAE = A.NOME_DEMANDA
			  AND DATA_CADASTRAMENTO_MAE = A.DATA_CADASTRAMENTO ) B
WHERE FLAG_ROOT = 1 
AND FLAG_ATRELADO_MARCO = 0
AND A.NOME_PROJETO = 'Inteligência de Negócios'
AND A.STATUS_DEMANDA <> 'Cancelada'