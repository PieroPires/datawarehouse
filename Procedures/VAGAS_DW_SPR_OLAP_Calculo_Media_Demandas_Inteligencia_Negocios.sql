USE VAGAS_DW
GO

IF EXISTS ( SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLAP_Calculo_Media_Demandas_Inteligencia_Negocios' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE VAGAS_DW.SPR_OLAP_Calculo_Media_Demandas_Inteligencia_Negocios
GO

-- =============================================
-- Author: Luiz Fernando Braz
-- Create date: 20/08/2016
-- Description: Procedure para cálculo da MEDIA_DIAS_ENTREGA para a equipe IN
-- =============================================

CREATE PROCEDURE VAGAS_DW.SPR_OLAP_Calculo_Media_Demandas_Inteligencia_Negocios 
AS
SET NOCOUNT ON

-- ZERAR MÉDIA PARA PODER CRIAR A MÉDIA MÓVEL
UPDATE VAGAS_DW.DEMANDAS_EQUIPES SET MEDIA_DIAS_ENTREGA = NULL,DATA_ULT_ENTREGA = NULL
FROM VAGAS_DW.DEMANDAS_EQUIPES 
WHERE EQUIPE_PROJETO = 'Inteligência de Negócios'

-- MÉDIA MÓVEL (ÚLTIMOS 6 MESES)
-- APENAS DEMANDAS CONCLUIDAS 	  
-- SE A DEMANDA FOR FECHADA EM MENOS DE 10 MINUTOS CONSIDERAREMOS 4 DIAS (visando diminuir o impacto de demandas que não foram cadastradas no dia correto )
UPDATE VAGAS_DW.DEMANDAS_EQUIPES SET MEDIA_DIAS_ENTREGA = CASE WHEN DATEDIFF(MINUTE,A.DATA_CADASTRAMENTO,ISNULL(A.DATA_CONCLUSAO,GETDATE())) < 10 THEN 4 
			ELSE DATEDIFF(DAY,A.DATA_CADASTRAMENTO,ISNULL(A.DATA_CONCLUSAO,GETDATE())) END 
FROM VAGAS_DW.DEMANDAS_EQUIPES A
WHERE A.EQUIPE_PROJETO = 'Inteligência de Negócios'
AND A.FLAG_ROOT = 1
AND A.STATUS_DEMANDA = 'Done' 
AND A.NOME_DEMANDA NOT LIKE '%MARCO%'
AND A.NOME_DEMANDA NOT LIKE '%CICLO%'
AND A.DATA_CADASTRAMENTO >= DATEADD(MONTH,-6,GETDATE())
AND EXISTS ( SELECT * 
			 FROM VAGAS_DW.DEMANDAS_EQUIPES 
			 WHERE NOME_DEMANDA_ROOT = A.NOME_DEMANDA_ROOT
			 AND CICLO = A.CICLO
			 AND FLAG_ROOT = 0
			 AND TIPO_SUB_TAREFA IN ('Cubo','Relatório','Relatório pontual','Mailing','Estudos para Assessoria',
								  'Apresentação de resultados','Desenho do questionário')
			 AND STATUS_DEMANDA <> 'Cancelada' )

-- ATUALIZAR MEDIA, DATA ULT ENTREGA E DIAS PARA CONCLUSÃO DAS TAREFAS MAE
UPDATE VAGAS_DW.DEMANDAS_EQUIPES SET DATA_ULT_ENTREGA = B.DATA_CONCLUSAO
FROM VAGAS_DW.DEMANDAS_EQUIPES A
OUTER APPLY ( SELECT TOP 1 * 
			  FROM VAGAS_DW.DEMANDAS_EQUIPES 
			  WHERE NOME_DEMANDA_ROOT = A.NOME_DEMANDA_ROOT
			  AND FLAG_ROOT = 0
			  AND STATUS_DEMANDA <> 'Cancelada' 
			  ORDER BY DATA_CONCLUSAO DESC ) B
WHERE A.FLAG_ROOT = 1 
AND A.NOME_PROJETO = 'Inteligência de Negócios'
