USE [STAGE]
GO

IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLTP_Carga_Freshdesk' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE VAGAS_DW.SPR_OLTP_Carga_Freshdesk
GO

-- =============================================
-- Author: Diego Gatto
-- Create date: 29/10/2019
-- Description: Procedure para carga das tabelas temporárias (BD Stage) para alimentação do DW
-- =============================================

CREATE PROCEDURE VAGAS_DW.SPR_OLTP_Carga_Freshdesk

AS
SET NOCOUNT ON

DECLARE @TimeZone VARCHAR(50)
EXEC MASTER.dbo.xp_regread 'HKEY_LOCAL_MACHINE',
'SYSTEM\CurrentControlSet\Control\TimeZoneInformation',
'TimeZoneKeyName',@TimeZone OUT

/*
	OBTÉM AS POSIÇÕES ATUAIS DE ATUALIZAÇÃO
*/
DECLARE  @ULT_ATUAL_TICKETS VARCHAR(50)
		,@ULT_ATUAL_CONTATOS VARCHAR(50)

SELECT @ULT_ATUAL_TICKETS = CONVERT(varchar(50), CAST(MAX(DATA_ATUALIZACAO) AT TIME ZONE @TimeZone AT TIME ZONE 'UTC' AS DATETIME), 127)  FROM VAGAS_DW.VAGAS_DW.FRESHDESK_TICKETS

SELECT @ULT_ATUAL_CONTATOS = CONVERT(varchar(50), CAST(MAX(DATA_ATUALIZACAO) AT TIME ZONE @TimeZone AT TIME ZONE 'UTC' AS DATETIME), 127)  FROM VAGAS_DW.VAGAS_DW.FRESHDESK_CONTATOS

/*
	EXECUTA SCRIPT PYTHON PARA CARGA DAS TABELAS TEMPORÁRIAS
*/

TRUNCATE TABLE VAGAS_DW.TMP_FRESHDESK_TICKETS
TRUNCATE TABLE VAGAS_DW.TMP_FRESHDESK_AGENTES
TRUNCATE TABLE VAGAS_DW.TMP_FRESHDESK_CONTATOS
TRUNCATE TABLE VAGAS_DW.TMP_FRESHDESK_CONVERSAS

DECLARE	 @CMD VARCHAR(8000)
		,@MSG VARCHAR(8000)


DROP TABLE IF EXISTS #TMP_LOG_ERRO

CREATE TABLE #TMP_LOG_ERRO (ID_LOG_ERRO SMALLINT IDENTITY PRIMARY KEY ,
								ERRO VARCHAR(8000)) 

SET @CMD = 'Z:\Scripts\Python3\python Z:\Scripts\Scripts_Python\IntegraFreshdesk.py "' + @ULT_ATUAL_TICKETS + '" "' + @ULT_ATUAL_CONTATOS + '"'

INSERT INTO #TMP_LOG_ERRO (ERRO)
EXEC MASTER.DBO.XP_CMDSHELL @CMD


/*
	AJUSTE DOS HORÁRIOS PARA O FUSO ATUAL
*/

UPDATE VAGAS_DW.TMP_FRESHDESK_AGENTES
SET DATA_ATUALIZACAO = CAST((DATA_ATUALIZACAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)
	,DATA_CRIACAO  = CAST((DATA_CRIACAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)

UPDATE VAGAS_DW.TMP_FRESHDESK_CONTATOS
SET DATA_ATUALIZACAO = CAST((DATA_ATUALIZACAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)
	,DATA_CRIACAO  = CAST((DATA_CRIACAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)

UPDATE VAGAS_DW.TMP_FRESHDESK_TICKETS
SET DATA_ATUALIZACAO = CAST((DATA_ATUALIZACAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)
	,DATA_CRIACAO  = CAST((DATA_CRIACAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)
	,DATA_FECHAMENTO = CAST((DATA_FECHAMENTO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)
	,DATA_PRIMEIRA_RESPOSTA = CAST((DATA_PRIMEIRA_RESPOSTA AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)
	,DATA_RESOLUCAO = CAST((DATA_RESOLUCAO AT TIME ZONE 'UTC' ) AT TIME ZONE @TimeZone AS DATETIME2)