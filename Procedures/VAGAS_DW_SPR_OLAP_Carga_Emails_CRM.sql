USE VAGAS_DW
GO

IF EXISTS ( SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLAP_Carga_Emails_CRM' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE VAGAS_DW.SPR_OLAP_Carga_Emails_CRM
GO

-- =============================================
-- Author: Luiz Fernando Braz
-- Create date: 24/08/2016
-- Description: Procedure para carga das tabelas temporárias (BD Stage) para alimentação do DW
-- =============================================

CREATE PROCEDURE VAGAS_DW.SPR_OLAP_Carga_Emails_CRM 
AS
SET NOCOUNT ON

-- Limpar dados da tabela fato
DELETE VAGAS_DW.EMAILS_CRM
FROM VAGAS_DW.EMAILS_CRM A
WHERE EXISTS ( SELECT 1 FROM VAGAS_DW.TMP_EMAILS_CRM
				WHERE ID_EMAIL = A.ID_EMAIL )

-- var Grupo
UPDATE VAGAS_DW.TMP_EMAILS_CRM SET GRUPO_VENDEDOR = B.GRUPO_VENDEDOR
FROM VAGAS_DW.TMP_EMAILS_CRM A
INNER JOIN VAGAS_DW.GRUPO_VENDEDORES B ON B.VENDEDOR = A.USUARIO_RESPONSAVEL

-- CLIENTE ASSOCIADO À OPORTUNIDADE
UPDATE VAGAS_DW.TMP_EMAILS_CRM SET CONTA = B.CONTA
FROM VAGAS_DW.TMP_EMAILS_CRM A
INNER JOIN VAGAS_DW.OPORTUNIDADES B ON B.OPORTUNIDADE = A.REFERENTE_A
WHERE A.TIPO_CONTATO = 'Opportunities'

UPDATE VAGAS_DW.TMP_EMAILS_CRM SET CONTA = A.REFERENTE_A
FROM VAGAS_DW.TMP_EMAILS_CRM A
WHERE A.TIPO_CONTATO IN ('Accounts','Leads')

INSERT INTO VAGAS_DW.EMAILS_CRM (ID_EMAIL,NOME,DATA_CADASTRO,DATA_ALTERACAO,USUARIO_CADASTRO,USUARIO_ALTERACAO,USUARIO_RESPONSAVEL,
								 DATA_ENVIO,STATUS,TIPO,CLASSIFICACAO,REFERENTE_A,GRUPO_VENDEDOR,TIPO_CONTATO,CONTA)
SELECT ID_EMAIL,NOME,DATA_CADASTRO,DATA_ALTERACAO,USUARIO_CADASTRO,USUARIO_ALTERACAO,USUARIO_RESPONSAVEL,DATA_ENVIO,
	   CASE WHEN STATUS = 'unread' THEN 'Não Lido'
			WHEN STATUS = 'read' THEN 'Lido'
			WHEN STATUS = 'Email cadastrado como NOTA' THEN STATUS
			ELSE 'Não Classificado' END,
	   CASE WHEN TIPO = 'inbound' THEN 'Recebido'
			WHEN TIPO = 'out' THEN 'Enviado'
			ELSE 'Não Classificado' END,
	   CASE WHEN CHARINDEX('#Prospecção',NOME) > 0 THEN 'Prospecção'
			WHEN CHARINDEX('#Relacionamento',NOME) > 0 THEN 'Relacionamento'
			ELSE 'Não Classificado' END,
	  REFERENTE_A,
	  GRUPO_VENDEDOR,
	  TIPO_CONTATO,
	  CONTA
FROM VAGAS_DW.TMP_EMAILS_CRM
WHERE FLAG_DELETADO = 0



