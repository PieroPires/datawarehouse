-- =============================================
-- Author: Fiama
-- Create date: 28/08/2018
-- Description: Procedure para alimentação do DW
-- =============================================

USE [VAGAS_DW] ;

IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE NAME = 'SPR_OLAP_Carga_SNAPSHOT_CLIENTES_INTENSIDADE' AND SCHEMA_NAME(SCHEMA_ID) = 'VAGAS_DW')
DROP PROCEDURE [VAGAS_DW].[SPR_OLAP_Carga_SNAPSHOT_CLIENTES_INTENSIDADE] ;
GO

CREATE PROCEDURE [VAGAS_DW].[SPR_OLAP_Carga_SNAPSHOT_CLIENTES_INTENSIDADE]
AS
SET NOCOUNT ON

-- Data de carga dos dados:
DECLARE	@DATA_CARGA SMALLDATETIME = CAST(GETDATE() AS DATE) ;

-- Remove a duplicidade de registros, para uma mesma DATA_CARGA:
DELETE FROM [VAGAS_DW].[SNAPSHOT_CLIENTES_INTENSIDADE]
FROM	[VAGAS_DW].[SNAPSHOT_CLIENTES_INTENSIDADE] AS A
WHERE	A.DATA_CARGA = @DATA_CARGA ;


INSERT INTO [VAGAS_DW].[SNAPSHOT_CLIENTES_INTENSIDADE] (DATA_CARGA,CONTA_CRM,INTENSIDADE,RISCO_RESCISAO,PRINCIPAL_MOTIVO_RISCO,OBS_DET_RISCO,CRITERIO_SUCESSO)
SELECT	@DATA_CARGA ,
		A.CONTA_CRM ,
		A.INTENSIDADE ,
		A.RISCO_RESCISAO ,
		A.PRINCIPAL_MOTIVO_RISCO ,
		A.OBS_DET_RISCO ,
		A.CRITERIO_SUCESSO
FROM	[VAGAS_DW].[CLIENTES_INTENSIDADE] AS A ;


-- Mantém a última data de carga no mês:
DELETE FROM [VAGAS_DW].[SNAPSHOT_CLIENTES_INTENSIDADE]
FROM	[VAGAS_DW].[SNAPSHOT_CLIENTES_INTENSIDADE] AS A
WHERE	EXISTS (SELECT	1
				FROM	[VAGAS_DW].[SNAPSHOT_CLIENTES_INTENSIDADE] AS A1
				WHERE	DATEPART(YEAR, A.DATA_CARGA) = DATEPART(YEAR, A1.DATA_CARGA)
						AND DATEPART(MONTH, A.DATA_CARGA) = DATEPART(MONTH, A1.DATA_CARGA)
						AND DATEPART(DAY, A.DATA_CARGA) < DATEPART(DAY, A1.DATA_CARGA)) ;